import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.vitaliimalone.a10secondschat"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        dataBinding {
            enabled = true
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "-SNAPSHOT"
            resValue("string", "app_name", "10 Seconds Chat (Debug)")
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("string", "app_name", "10 Seconds Chat")
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}-${buildTime()}.apk"
        }
    }
}

androidExtensions {
    experimental = true
}

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

dependencies {
    def lifecycle_version = '2.0.0'
    def room_version = '2.1.0-alpha04'
    def koin_version = '1.0.2'
    def work_manager_version = '1.0.0-beta05'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // core
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.1.0-alpha02"
    implementation "androidx.core:core-ktx:1.1.0-alpha04"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"
    implementation "com.google.android.material:material:1.1.0-alpha03"
    implementation "androidx.recyclerview:recyclerview:1.0.0"

    // db
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"

    // rx
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"

    // di
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // work
    implementation "android.arch.work:work-runtime-ktx:$work_manager_version"
    implementation "android.arch.work:work-rxjava2:$work_manager_version"

    // test
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.1.2-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.2-alpha01"
}
